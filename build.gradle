buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id "com.jfrog.bintray" version "1.4"
}

group = 'com.github.splatte'
version = '2.0'

apply plugin: 'groovy'
apply plugin: 'maven-publish'

dependencies {
    repositories {
        mavenCentral()
    }

    compile gradleApi()
    compile localGroovy()
    compile 'com.android.tools.build:gradle:1.0.+'
}

bintray {
    user = bintray_user
    key = bintray_api_key

    publications = ['main']

    dryRun = false
    publish = false /* need to publish new version manually on https://bintray.com/splatte/maven/gradle-android-appiconoverlay */

    pkg {
        repo = 'maven'
        name = 'gradle-android-appiconoverlay'
        desc = 'Plugin for Android Gradle to automatically overlay the app icon with information about the current build.'
        websiteUrl = 'https://github.com/splatte/gradle-android-appiconoverlay'
        issueTrackerUrl = 'https://github.com/splatte/gradle-android-appiconoverlay'
        vcsUrl = 'https://github.com/splatte/gradle-android-appiconoverlay'
        licenses = ['Apache-2.0']
        labels = ['android', 'gradle', 'app', 'icon', 'build']
        publicDownloadNumbers = false

        version {
            name = '2.0'
        }
    }
}

tasks.withType(JavaCompile) {
    sourceCompatibility = '1.7'
    targetCompatibility = '1.7'
}

tasks.withType(GroovyCompile) {
    sourceCompatibility = '1.7'
    targetCompatibility = '1.7'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        main(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}
